#class dazuo open
#var min_dazuo_count 60;
#var dz_total_count 0;
#var dz_max 95;
#alias {dz.cancel} {
    #class dz.full.inner kill;
    #unticker {dz};
};

#alias {dz.idle} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #action {你的内力增加了} {
        #class dz.full.inner kill;
        #delay {dz_delay} {
            tmp_dz;
        } {3};
    };
    #ticker {dz} {
        yun recover; dazuo ${dazuo_count};
    } {1};
    #class dz.full.inner close;
    #math max_neili ${youxiao_neigong} * 10 - 10;
    #if {${max_neili} < ${hpbrief_max_nl}} {
        #class dz.full.inner kill;
        #delay {dz_delay} {
            tmp_dz;
        } {20};
    };
};

#alias {dz.full} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #class dz.full.inner close;
    #delay {dz_delay} {
        #ticker {dz} {
            #if {${nl_percent} < ${dz_max}} {
                yun recover; dazuo ${dazuo_count};
            };
            #else {
                #unticker {dz};
                #class dz.full.inner kill;
                #delay {dz_delay} {
                    tmp_dz;
                    #showme dz.full;
                } {3};
            };
        } {1};
    }{1};
};

#alias {dz.start} {
    set_last_quest dz;
    autoheal.off;
    #class dz.inner open;
    #alias {start_ticker} {
        #ticker {dz} {
            dazuo ${dazuo_count};
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {dz};
    };

    #action {你现在的气太少了} {
        stop_ticker;
        sleep;
        on_wakeup {
            start_ticker;
        };
    };

    #action {你的内力增加了} {
        #math dz_total_count ${dz_total_count}+1;
        show_to_info force +${dz_total_count};
        #send {jifa};
        #delay {2} {
            dz.check;
        };
    };
    #class dz.inner close;
    #send {jifa};
    gt bs;
    on_there {
        dz.check;
    };
};

#alias {dz.check} {
    stop_ticker;
    chifan;
    burden.check {
        dz.check.inner;
    };
};

#alias {dz.check.inner} {
    #math max_neili ${youxiao_neigong} * 10 -1;
    #if {${max_neili} < ${hpbrief_max_nl}} {
        dz.stop;
        #showme dz done;
        show_to_quest 内力到达瓶颈: ${hpbrief_max_nl} => ${max_neili};
    };
    #else {
        #if {@getburden{酥油茶} < 30} {
            dz.stop;
            chain get.qian get.fan dz.start;
            #return;
        };
        start_ticker;
        show_to_quest 打坐: ${hpbrief_max_nl} => ${max_neili};
    };
};

#alias {dz.stop} {
    unset_last_quest;
    autoheal.on;
    stop_ticker;
    show_to_quest 打坐完毕;
    #class dz.inner kill;
};

#var dz_dwim 100;
#alias {dz.dwim} {
    #if {${nl_percent} < 50} {
        #math dz_dwim ${dz_dwim} + ${hpbrief_max_nl}/200;
    };
    #else {
        #math dz_dwim ${dz_dwim} - ${hpbrief_max_nl}/500;
    };
    #if {${dz_dwim} < 10} {
        #var dz_dwim 100;
    };
    dazuo ${dz_dwim};
};
#class dazuo close