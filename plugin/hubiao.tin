#class hubiao open
load hubiao_score;

#var hb_auto 0;
#var hb_rotate 0;
#var hb_dest nil;

#var getesc_current_score 0;
#var getesc_current_id 0;

#var hb_first_kill 0;
#var hb_paused 0;
#var hb_info nil;
#var hb_rotate_location NIL;
#var hb_rotate_location_count 0;
#var hb_robber_count 0;

#alias {hb.teardown} {
    #var quest NIL;
    #var hb_robber_count 0;
    #class hb_setup_inner kill;
    #class clear_npc.inner kill;
    #unticker {hb_ticker};
};

#alias {hb.traverse} {
    #alias {auto_pp_done} {
        #var gps_traverse_location %%1;
        #alias {gps_check_room_done} {
            #if {${gps.roomno} == -1} {
                #delay {1} {
                    #show {GPS: 当前房间未记录, 无法遍历};
                };
            };
            #else {
                show_to_info hb.traverse ${gps_traverse_location};
                INVOKE gps.traverse ${gps.roomno} ${gps_traverse_location};
                #var hb_path ${gps_path};
                #var hb_path_index 1;
                #list hb_path size hb_path_size;
                hb.run;
            };  
        };
        gps.check_room {
            gps_check_room_done;
        };
    };

    autopp {
        auto_pp_done;
    };
};

#alias {hb.run} {
    #list hb_path get ${hb_path_index} hb_next_step;
    #if {@startwith{${hb_next_step};gps}} {
        #replace {hb_next_step} {gps} {hb};
    };
    #else {
        #var hb_next_step gan che to ${hb_next_step};
    };
    #if {${qx_percent} < 50 || ${js_percent} < 50 || ${nl_percent} < 20} {
        #delay {hb_move} {
            show_to_info 气血或精神过低,推车暂停;
            notify escort paused;
            on_truce {
                on_health {hb.run};
            };
        } {3};
    };
    #else {
        #if {${hb_path_index} > ${hb_path_size}} {
            #delay {hb_move} {
                #if {"@getenv{hb_state}" == "STARTED"} {
                    @setenv{hb_state;TRAVERSE}
                    hb.traverse;
                };
                #else {
                    hb.pause;
                    show_to_info 到达;
                    notify.alert escort arrived;
                    xmpp.notify 到达, 没找到收货人;
                    pp;
                };
            } {3};
        };
        #else {
            #if {${eff_qx_percent} < 95} {
                #2 yun heal;
            };
            #if {${eff_qx_percent} < 50} {
                #2 yun lifeheal;
            };
            #delay {hb_move} {
                #if {${hb_paused} == 0} {
                    ${hb_next_step};
                };
            } {3};
        };
    };
};

#var hb_retry_count 0;
#alias {hb.setup} {
    #var quest hb;
    #class hb_setup_inner open;

    #alias {hb.guohe} {
        #class guohe.inner open;
        #ticker {sw/yell_boat} {
            yell boat;
        } {5};
        #action {正等着你呢，上来吧。} {
            gan che to enter;
        };
        #action {艄公将一块踏脚板搭上堤岸，} {
            gan che to enter;
        };
        #alias {shangchuan} {
            #unticker {sw/yell_boat};
            #action {所有人被赶下了渡船} {
                #unaction {所有人被赶下了渡船};
                #class guohe.inner kill;
                #math hb_path_index ${hb_path_index}+1;
                #var hb_paused 0;
                hb.run;
            };
        };
        #action {^黄河渡船$} {
            shangchuan;
        };
        #action {^长江渡船$} {
            shangchuan;
        };
        #class guohe.inner close;

        hb.pause;
        ask shao gong about huanghe;
        ask shao gong about jiang;
    };
    #alias {hb.delay} {
        #var gps_delay 1.5;
        #var gps_delay %1;
        #delay {${gps_delay}} {
            hb.run;
        };
    };
    #alias {hb.clear} {
        #var tmp_blocker %1;
        #class clear_blocker.inner open;
        #action {你想杀谁} {
            #class clear_blocker.inner kill;
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
        #ticker {tmp_blocker_ticker_kill} {
            killall ${tmp_blocker};
        } {5};
        #class clear_blocker.inner close;
        hb.pause;
        killall ${tmp_blocker};
    };

    #action {看来该找机会逃跑了} {
        hb.pause;
        show_to_tuiche 丢镖;
        xmpp.notify 丢镖;
    };

    #action {^慢慢地你终于又有了知觉....} {
        show_to_tuiche 挂了;
        notify.alert 挂了;
        xmpp.notify 挂了;
        hb.teardown;
        hb.b;
    };

    #action {^你的眼前一黑，接著什么也不知道了} {
        hb.pause;
    };

    #action {你想杀谁？} {
        hb.retry;
    };

    #action {镖车还没有跟上来呢,走慢点.} {
        hb.retry;
    };
    
    #action {你还是先把对手解决了再说吧} {
        #math hb_retry_count ${hb_retry_count}+1;
        #if {${hb_retry_count} > 3} {
            #var hb_retry_count 0;
            killall;
        };
        hb.retry;
    } {1};

    #action {镖车呼啦一声散了架} {
        hb.pause;
        show_to_tuiche 镖车呼啦一声散了架;
        hb.b;
    };

    #action {busy_for_move} {
        hb.retry;
    };

    #action {^你赶着镖车驶了过来。$} {
        #var hb_retry_count 0;
        #if {${hb_paused} == 0} {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #action {劫匪个起纵遁入暗里不见了} {
        hb.detect_enemy;
        raw;
        hb.retry;
    };

    #action {^这个方向} {
        #if {${hb_paused} == 0} {
            hb.pause;
            hb.resume;
        };
    };

    #action {劫匪趁你不注意，推着镖车就跑，你赶紧追了上去。} {
        hb.pause;
        @setenv{mixin_lunci;6};
        #delay {hb_delay} {
            hb.resume;
        } {3};
    };

    #action {劫匪伸手一拦道} {
        war;
        hb.retry;
    };

    #action {你长笑一声，开始对周围众人痛下杀手！} {
        #var hb_first_kill 1;
    };

    #action {劫匪突然从暗处跳了出来} {
        hb.detect_enemy;
        #if {${hb_first_kill} == 0} {
            killall robber;
        };
        #else {
            war;
        };
    };

    #action {围上来几个伙计将镖车拉了进去} {
        @setenv{hb_state;ENDED};
        hb.pause;
        raw;
        show_to_tuiche 任务完成;
        notify escort done;
        hb.teardown;
        on_truce {
            hb.b;
        };
    } {1};

    #class hb_setup_inner close;

    #var hb_elapsed_time 0;
    #ticker {hb_ticker} {
        #math hb_elapsed_time ${hb_elapsed_time}+1;
        show_to_tuiche ${hb_info} --> ${hb_elapsed_time}:00;
    } {60};
};

#alias {hb.retry} {
    #if {${hb_paused} == 0} {
        #delay {hb_move} {
            hb.run;
        } {1.5};
    };
};

#alias {hb.pause} {
    #var hb_paused 1;
    #undelay {hb_move};
};

#alias {hb.get_owner} {
    #alias {get_pos_done} {
        #if {"${pos.room}" == "昌隆镖局"} {
            @setenv{hb_owner;zuo;}
        }
    };
    get_pos {
        get_pos_done;
    };
};

#alias {hb.d} {
    #if {"@getenv{hb_state}" == "GOT_CAIWU"} {
        give cai wu to @getenv{hb_owner};
    };
    #else {
        #if {@getenv{hb_lunci} == @getenv{mixin_lunci}} {
            @setenv{hb_lunci;0};
            notify.alert next mixin;
            xmpp.notify next mixin;
            play alert.wav;
        };
        #else {
            ask @getenv{hb_owner} about finish;
        };
    };
};

#alias {hb.q} {
    #if {"%1" != ""} {
        getesc %1;
    };
    #else {
        listesc;
    }
};


#alias {hb.f} {
    @setenv{hb_lunci;0};
    @setenv{hb_lunci_total;0};
    @setenv{hb_state;NIL};
    @setenv{mixin_lunci;6};
    hb.teardown;
    ask @getenv{hb_owner} about fail;
};

#alias {hb.b} {
    #var hb_owner @getenv{hb_owner};
    #if {"${hb_owner}" == "zuo"} {
        gt zuo;
    };
    #elseif {"${hb_owner}" == "zhang"} {
        gt zz;
    };
    #elseif {"${hb_owner}" == "ma"} {
        gt fm;
    };
    #elseif {"${hb_owner}" == "wang"} {
        gt tf;
    };
    #elseif {"${hb_owner}" == "chu"} {
        gt fh;
    };

    on_there {
        hb.d;
    };
};

#alias {hb.start} {
    set brief 2;
    #var hb_auto 1;

    #class hb.auto.inner open;
    #action {^你不是才要了任务吗} {
        hb.f;
        on_unbusy {
            hb.start;
        };
    };

    #action {你一共被奖励了：} {
        #class tmp.inner open;
        #action {%*点经验；} {
            #class tmp.inner kill;
            show_to_info @ctd{%%%1} 点经验;
        };
        #delay {hb_auto_delay} {
            #if {${hb_auto} == 1} {
                hb.start;
            };
        } {3};
        #class tmp.inner close;
    };

    #action {${char_name}帮%*追回财物，额外获得%*点经验} {
        show_to_info 额外获得 @ctd{%%2} 点经验;
    };

    #alias {got_gem} {
        #action {你将手中%*{◎|☆|★}%*放进了宝石袋} {
            show_to_info 掉宝: %%%1%%%2%%%3;
            notify.info %%%1%%%2%%%3;
            gem.record %%%1%%%2%%%3;
            #var gem_level @gemlevel{%%%1};
            play gem2.wav;
            xmpp.notify 掉宝: %%%1%%%2%%%3;
        };
        pack gem;
        #delay {pack_gem} {
            #unaction {你将手中%*{◎|☆|★}%*放进了宝石袋};
        } {3};
    };

    #action {哇，你太厉害了，这么快就把这些财物追回，我有重谢！} {
        got_gem;
    };

    #action {掉了出来一颗} {
        got_gem;
    };

    #action {%d%s%S%s%S%s待认领} {
        #var getesc_id %%1;
        #var getesc_address %%3;
        #var score @getesc_get_score{${getesc_address}};
        #if {${score} > ${getesc_current_score}} {
            #var getesc_current_score $score;
            #var getesc_current_id ${getesc_id};
        };
    };

    #action {来认领押镖任务} {
        #if {${getesc_current_id} != 0} {
            #var hb_rotate_location_count 0;
            hb.q ${getesc_current_id};
        };
        #else {
            #math hb_rotate_location_count ${hb_rotate_location_count} + 1;
            #if {${hb_rotate} == 1 && ${hb_rotate_location_count} > 3 && "${hb_rotate_location}" != "NIL"} {
                #var hb_rotate_location_count 0;
                hb.stop;
                gt ${hb_rotate_location};
                on_there {
                    hb.start;
                };
            };
            #else {
                #delay {hb_auto_delay} {
                    hb.start;
                } {10};
            };
        };
    };

    #action {请选择其他任务} {
        #delay {hb_auto_delay} {
            #var getesc_current_score 0;
            #var getesc_current_id 0;
            hb.q;
        } {2};
    };

    #action {你上次运镖太辛苦了} {
        #delay {hb_auto_delay} {
            hb.q;
        } {10};
    };

    #action {你所领的任务在任务列表中并不存在} {
        #delay {hb_auto_delay} {
            hb.q;
        } {2};
    };

    #action {${char_name}把这批红货送到%*他已经派了个伙计名叫%*到%*附近接你} {
        @setenv{hb_state;STARTED};
        @setenv{hb_lunci;@eval{@getenv{hb_lunci}+1}};
        INVOKE gps.get_area %%1;
        #var hb_dest ${gps.area}%%3;
        #var hb_info %%2|${hb_dest};
        show_to_tuiche ${hb_info};
        pp %%2;

        hb.setup;
        hb.resume;
    } {1};

    #action {递给你一封密信} {
        @setenv{hb_state;GOT_MIXIN};
        @setenv{hb_lunci_total;@eval{@getenv{hb_lunci_total}+1}};
        @setenv{hb_lunci;0};
        show_to_tuiche 密信;
        mx.o;
    };

    #class hb.auto.inner close;

    @declarenv{hb_state;NIL};
    @declarenv{mixin_lunci;6};

    hb.get_owner;

    #if {"@getenv{hb_state}" == "GOT_CAIWU"} {
        show_to_info 交财物先;
    };
    #if {"@getenv{hb_state}" == "GOT_MIXIN"} {
        show_to_info 密信;
    };
    #elseif {"@getenv{hb_state}" == "STARTED"} {
        hb.f;
    };
    #else {
        #math next_lunci @getenv{hb_lunci}+1;
        show_to_quest 推车: @getenv{hb_lunci_total}+${next_lunci}: 准备;
        on_health {hb.q};
    };
};


#alias {hb.stop} {
    #var hb_auto 0;
    #class hb.auto.inner kill;
    #undelay {hb_auto_delay};
    dz.cancel;
};

#alias {mx.o} {
    xian mixin;
};

#alias {mx.re} {
    give mixin to @getenv{hb_owner};
    mx.o;
};

#alias {mx.q} {
    #class mx.inner open;
    #action {一个伙计挖着鼻屎走了出来，道：你找我啥事} {
        l;
    };
    #action {财物的伙计 %S(%S)} {
        #format {tmp_huoji} {%l} {%%2};
        #class mx.yao.inner open;
        #action {把一包财物砸向你，一转眼不见了。} {
            @setenv{hb_state;GOT_CAIWU};
            show_to_tuiche 拿到财宝;
            play get_item.wav;
            #class mx.yao.inner kill;
            #class mx.inner kill;
            back;
        };
        #action {这里没有这个人} {
            #class mx.yao.inner kill;
        };
        #ticker {yao} {
            ask $tmp_huoji about yao;
        }{0.5};
        #class mx.yao.inner close;
    } {1};
    #class mx.inner close;
    zhaoxun;
};

#alias {mx.d} {
    give cai wu to @getenv{hb_owner};
    #class mx.inner kill;
};

#alias {hb.p} {
    chain get.san get.qian get.fan get.bao repair;
};

#action {你给%*一些失踪的财物} {
    @setenv{hb_state;NIL};
    @setenv{mixin_lunci;5};
    #delay {hb_auto_delay} {
        #if {${hb_auto} == 1} {
            hb.start;
        };
    } {3};
};

#alias {show_to_tuiche} {
    show_to_quest 推车: @getenv{hb_lunci_total}+@getenv{hb_lunci}: %0;
};

#alias {got_robber} {
    #delay {detect_enemy_delay} {
        #var hb_robber_count ${tmp_robber_count};
        #class detect_enemy.inner kill;
    } {1};
    #math tmp_robber_count ${tmp_robber_count}+1;
    #var tmp_score @mapget{robber_score;%1};
    #if {${tmp_score} > ${robber_score}} {
        #var robber_score ${tmp_score};
        #var enemy ${char_id}'s robber ${tmp_robber_count};
    };
};

#alias {hb.resume} {
    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "STARTED"} {
        hb.get_path_and_run;
    };
    #elseif {"${hb_state}" == "TRAVERSE"} {
        hb.traverse;
    };
};

#alias {hb.get_path_and_run} {
    #var hb_paused 0;
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 当前房间未记录, 无法行走};
            };
        };
        #else {
            #var gps.from_roomno ${gps.roomno};
            INVOKE gps.check_room ${hb_dest};
            INVOKE gps.get_path ${gps.from_roomno} ${gps.roomno} "1,1,1,-1,-1,1";
            #var hb_path ${gps_path};
            #var hb_path_index 1;
            #list hb_path size hb_path_size;
            #if {${hb_path_size} == 0} {
                #delay {1} {
                    #show {GPS: 目标房间不可达, 无法行走};
                };
            };
            #else {
                hb.run;
            };
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {hb.detect_enemy} {
    #class detect_enemy.inner open;
    #action {~[1;31m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber quanzhen;
    };
    #action {~[1;32m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber shaolin;
    };
    #action {~[1;33m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber gaibang;
    };
    #action {~[1;34m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber xingxiu;
    };
    #action {~[1;35m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber huashan;
    };
    #action {~[1;36m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber taohua;
    };
    #action {~[1;37m劫匪[2;37;0m(@capital{${char_id}}'s robber)} {
        got_robber wudang;
    };
    #action {风景要慢慢的看} {
        #delay {0.5} {
            l;
        };
    };
    #class detect_enemy.inner close;
    #undelay {detect_enemy_delay};
    #var enemy NULL;
    #var tmp_robber_count 0;
    #var hb_robber_count 0;
    #var robber_score 0;
    l;
};
#class hubiao close
