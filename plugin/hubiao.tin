#class hubiao open
#var hb_max_path_size 45;
#var hb_auto 0;
#var hb_rotate 0;
#var hb_dest nil;
#var hb_first_kill 0;
#var hb_paused 0;
#var hb_info nil;
#var hb_rotate_location nil;
#var hb_rotate_location_count 0;
#var hb_robber_count 0;

#var HB_WEIGHT "1,1,1,-1,-1,1,-1,-1";

@mapcreate{robber_score};
@mapset{robber_score;gaibang;9};
@mapset{robber_score;taohua;8};
@mapset{robber_score;wudang;7};
@mapset{robber_score;xingxiu;6};
@mapset{robber_score;quanzhen;4};
@mapset{robber_score;shaolin;3};
@mapset{robber_score;huashan;2};

#function {should_accept_quest} {
    #var hb_owner_loc @getenv{hb_owner_loc};
    #if {@ismember{%1;hb_blacklist:${hb_owner_loc}} != 0} {
        #return 0;
    };
    #if {"${xy_info}" == "on" && {@ismember{%1;hb_blacklist_baowei:${hb_owner_loc}} != 0}} {
        #return 0;
    };
    #return 1;
};

#alias {hb.teardown} {
    #var quest nil;
    #var hb_robber_count 0;
    #class hb_setup_inner kill;
    #class clear_npc.inner kill;
    #unticker {hb_ticker};
    set brief 2;
};

#alias {hb.traverse} {
    #alias {auto_pp_done} {
        #var gps_traverse_location %%0;
        #alias {gps_check_room_done} {
            #if {${gps.roomno} == -1} {
                #delay {1} {
                    show_to_info 当前房间未记录, 无法遍历伙计;
                };
            };
            #else {
                INVOKE gps.traverse ${gps.roomno} "${gps_traverse_location}";
                #var hb_path ${gps_path};
                #var hb_path_index 1;
                #list hb_path size hb_path_size;
                #var hb_paused 0;
                #if {${hb_path_size} == 0} {
                    show_to_info 无法遍历伙计 @ ${gps_traverse_location};
                };
                #else {
                    hb.run;
                };
            };
        };
        #if {"${gps_traverse_location}" == "nil"} {
            show_to_info 伙计不存在;
            hb.b;
        };
        #else {
            gps.check_room {
                gps_check_room_done;
            };
        };
    };

    autopp {
        auto_pp_done;
    };
};

#alias {hb.run} {
    #list hb_path get ${hb_path_index} hb_next_step;
    #if {@startwith{${hb_next_step};gps}} {
        #replace {hb_next_step} {gps} {hb};
    };
    #elseif {@ismember{${hb_next_step};directions} != 0} {
        #var hb_next_step gan che to ${hb_next_step};
    };
    #else {
        #var hb_next_step hb.command ${hb_next_step};
    };

    #if {${qx_percent} < 30 || ${js_percent} < 30 || ${nl_percent} < 10} {
        hb.pause;
        #delay {hb_move} {
            on_truce {
                on_health {hb.resume};
            };
        } {3};
    };
    #else {
        #if {${hb_path_index} > ${hb_path_size}} {
            hb.pause;
            #delay {hb_move} {
                #if {"@getenv{hb_state}" == "STARTED"} {
                    @setenv{hb_state;TRAVERSE};
                };
                #if {"@getenv{hb_state}" == "STARTED" || "@getenv{hb_state}" == "TRAVERSE"} {
                    hb.resume;
                };
            } {5};
        };
        #else {
            #if {${eff_qx_percent} < 95} {
                #2 yun heal;
            };
            #if {${eff_qx_percent} < 50} {
                #2 yun lifeheal;
            };
            #delay {hb_move} {
                #if {${hb_paused} == 0} {
                    ${hb_next_step};
                };
            } {3};
        };
    };
};

#var hb_retry_count 0;
#alias {hb.setup} {
    set brief 1;
    #var quest hb;
    #var hb_remaining_path_size ${hb_max_path_size};
    #var hb_path_index 1;
    #class hb_setup_inner open;

    #alias {hb.guohe} {
        #class guohe.inner open;
        #ticker {sw/yell_boat} {
            yell boat;
        } {5};
        #action {正等着你呢，上来吧。} {
            gan che to enter;
        };
        #action {艄公将一块踏脚板搭上堤岸，} {
            gan che to enter;
        };
        #alias {shangchuan} {
            #unticker {sw/yell_boat};
            #action {所有人被赶下了渡船} {
                #unaction {所有人被赶下了渡船};
                #class guohe.inner kill;
                #math hb_path_index ${hb_path_index}+1;
                #var hb_paused 0;
                hb.run;
            };
        };
        #action {^黄河渡船$} {
            shangchuan;
        };
        #action {^长江渡船$} {
            shangchuan;
        };
        #class guohe.inner close;

        hb.pause;
        ask shao gong about huanghe;
        ask shao gong about jiang;
    };

    #alias {hb.command} {
        #var hb_command %%0;
        ${hb_command};
        #math hb_path_index ${hb_path_index}+1;
        hb.run;
    };

    #alias {hb.delay} {
        #var gps_delay 1.5;
        #var gps_delay %1;
        #delay {${gps_delay}} {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };
    #alias {hb.busy} {
        on_unbusy {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #alias {hb.clear} {
        #var hb_tmp_blocker %%0;
        #class clear_blocker.inner open;
        #action {你想杀谁} {
            #class clear_blocker.inner kill;
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
        #ticker {tmp_blocker_ticker_kill} {
            killall ${hb_tmp_blocker};
        } {5};
        #class clear_blocker.inner close;
        hb.pause;
        killall ${hb_tmp_blocker};
    };

    #action {看来该找机会逃跑了} {
        hb.pause;
        show_to_tuiche 丢镖;
        notify 丢镖;
        hb.teardown;
        hb.b;
    };

    #action {^慢慢地你终于又有了知觉....} {
        show_to_tuiche 挂了;
        notify 挂了;
        hb.teardown;
        hb.b;
    };

    #action {^你的眼前一黑，接著什么也不知道了} {
        hb.pause;
    };

    #action {你想杀谁？} {
        hb.retry;
    };

    #action {镖车还没有跟上来呢,走慢点.} {
        hb.retry;
    };

    #action {你还是先把对手解决了再说吧} {
        #math hb_retry_count ${hb_retry_count}+1;
        #if {${hb_retry_count} > 3} {
            #var hb_retry_count 0;
            killall;
        };
        hb.retry;
    } {1};

    #action {镖车呼啦一声散了架} {
        hb.pause;
        show_to_tuiche 镖车呼啦一声散了架;
        hb.b;
    };

    #action {busy_for_move} {
        hb.retry;
    };

    #action {^你赶着镖车驶了过来。$} {
        #var hb_retry_count 0;
        #if {${hb_paused} == 0} {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #action {劫匪个起纵遁入暗里不见了} {
        hb.detect_enemy;
        raw;
        hb.retry;
    };

    #action {^这个方向} {
        #if {${hb_paused} == 0} {
            hb.pause;
            hb.resume;
        };
    };

    #action {劫匪趁你不注意，推着镖车就跑，你赶紧追了上去。} {
        hb.pause;
        @setenv{mixin_lunci;6};
        #delay {hb_delay} {
            hb.resume;
        } {3};
    };

    #action {劫匪伸手一拦道} {
        war;
        hb.retry;
    };

    #action {你长笑一声，开始对周围众人痛下杀手！} {
        #var hb_first_kill 1;
    };

    #action {劫匪突然从暗处跳了出来} {
        hb.detect_enemy;
        #if {${hb_first_kill} == 0} {
            killall robber;
        };
        #else {
            war;
        };
    };

    #action {围上来几个伙计将镖车拉了进去} {
        @setenv{hb_state;ENDED};
        hb.pause;
        raw;
        show_to_tuiche 任务完成;
        hb.teardown;

        #alias {on_taopao_done} {
            #if {@getenv{hb_lunci} == @getenv{mixin_lunci}} {
                @setenv{hb_state;NEXT_MIXIN};
                chain sell.all get.qian get.san repair special_repair gem.combine hb.b;
            };
            #else {
                hb.b;
            };
        };

        taopao {
            on_taopao_done;
        };
    } {1};

    #class hb_setup_inner close;

    #var hb_elapsed_time 0;
    #ticker {hb_ticker} {
        #math hb_elapsed_time ${hb_elapsed_time}+1;
        show_to_tuiche ${hb_info} --> ${hb_elapsed_time}:00;
    } {60};
};

#alias {hb.retry} {
    #if {${hb_paused} == 0} {
        #delay {hb_move} {
            hb.run;
        } {1.5};
    };
};

#alias {hb.pause} {
    #var hb_paused 1;
    #undelay {hb_move};
};

#alias {hb.get_owner} {
    #action {%S %S总镖头 %*(%S %S)} {
        #unaction {%S %S总镖头 %*(%S %S)};
        @setenv{hb_owner;@lower{%%4 %%5}};
    };
    #alias {get_pos_done} {
        @setenv{hb_owner_loc;${pos.room}};
    };
    get_pos {
        get_pos_done;
    };
};

#alias {hb.d} {
    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "GOT_CAIWU"} {
        give cai wu to @getenv{hb_owner};
    };
    #elseif {"${hb_state}" == "ENDED"} {
        ask @getenv{hb_owner} about finish;
    };
    #elseif {"${hb_state}" == "NEXT_MIXIN"} {
        #if {@getenv{hb_lunci} == 0} {
            ask @getenv{hb_owner} about finish;
        };
        #else {
            @setenv{hb_lunci;0};
            notify 下一轮密信;
            play alert.wav;
        };
    };
    #elseif {"${hb_state}" == "GOT_MIXIN"} {
        give mixin to @getenv{hb_owner};
        xian mixin;
    };
    #elseif {"${hb_state}" == "nil"} {
        show_to_info 没有护镖任务;
    };
    #else {
        hb.f;
        #if {${hb_auto} == 1} {
            hb.start;
        };
    };
};

#alias {hb.q} {
    #var getesc_current_id 0;
    #if {"@getenv{hb_state}" == "nil"} {
        #if {"%1" != ""} {
            getesc %1;
        };
        #else {
            #ticker {hb_q_ticker} {
                listesc;
            } {5};
        };
    };
};


#alias {hb.f} {
    @setenv{hb_lunci;0};
    @setenv{hb_lunci_total;0};
    @setenv{hb_state;nil};
    @setenv{mixin_lunci;6};
    hb.teardown;
    ask @getenv{hb_owner} about fail;
};

#alias {hb.b} {
    gt @getenv{hb_owner_loc};
    on_there {
        hb.d;
    };
};

#alias {hb.start} {
    log.start;
    set_last_quest hb;
    xmpp.echo.off;
    set brief 2;
    #var hb_auto 1;

    #action {转头对你道：「辛苦了，有几两银子略表心意，已经吩咐人存入你的钱庄账户。」} {
        #if {"@getenv{hb_state}" == "ENDED"} {
            @setenv{hb_state;nil};
            #delay {hb_auto_delay} {
                #if {${hb_auto} == 1} {
                    hb.start;
                };
            } {3};
        };
    };

    #action {递给你一封密信} {
        @setenv{hb_state;GOT_MIXIN};
        @setenv{hb_lunci_total;@eval{@getenv{hb_lunci_total}+1}};
        @setenv{hb_lunci;0};
        show_to_tuiche 密信;
        xian mixin;
    };

    #action {你给%*一些失踪的财物} {
        @setenv{hb_state;nil};
        @setenv{mixin_lunci;5};
        #delay {hb_auto_delay} {
            #if {${hb_auto} == 1} {
                hb.start;
            };
        } {3};
    };

    #class hb.auto.inner open;
    #action {^你不是才要了任务吗} {
        hb.d;
        on_unbusy {
            hb.start;
        };
    };

    #action {你一共被奖励了：} {
        #class tmp.inner open;
        #action {%*点经验；} {
            #class tmp.inner kill;
            show_to_info @ctd{%%%1} 点经验;
        };
     #class tmp.inner close;
    };

    #action {${char_name}帮%*追回财物，额外获得%*点经验} {
        show_to_info 额外获得 @ctd{%%2} 点经验;
        do_show 密信完成;
    };

    #alias {got_gem} {
        #action {你将手中%*{◎|☆|★}%*放进了宝石袋} {
            notify %%%1%%%2%%%3;
            gem.record %%%1%%%2%%%3;
            #var gem_level @gemlevel{%%%1};
            play gem2.wav;
        };
        pack gem;
        #delay {pack_gem} {
            #unaction {你将手中%*{◎|☆|★}%*放进了宝石袋};
        } {3};
    };

    #action {哇，你太厉害了，这么快就把这些财物追回，我有重谢！} {
        got_gem;
        do_show 密信完成;
    };

    #action {掉了出来一颗} {
        got_gem;
    };

    #action {%d%s%S%s%S%s待认领} {
        #var getesc_id %%1;
        #var getesc_address %%3;
        #if {${getesc_current_id} == 0 && @should_accept_quest{${getesc_address}} == 1} {
            #var getesc_current_id ${getesc_id};
        };
    };

    #action {来认领押镖任务} {
        #var hb_state @getenv{hb_state};
        #if {"${hb_state}" == "nil"} {
            #if {${getesc_current_id} != 0} {
                #var hb_rotate_location_count 0;
                hb.q ${getesc_current_id};
            };
            #else {
                #math hb_rotate_location_count ${hb_rotate_location_count} + 1;
                #if {${hb_rotate} == 1 && ${hb_rotate_location_count} > 3 && "${hb_rotate_location}" != "nil"} {
                    #var hb_rotate_location_count 0;
                    hb.stop;
                    gt ${hb_rotate_location};
                    on_there {
                        hb.start;
                    };
                };
            };
        };
    };

    #action {${char_name}把这批红货送到%*他已经派了个伙计名叫%*到%*附近接你} {
        #unticker {hb_q_ticker};
        @setenv{hb_state;STARTED};
        @setenv{hb_lunci;@eval{@getenv{hb_lunci}+1}};
        @setenv{hb_huoji;%%2};
        INVOKE gps.get_area %%1;
        #var hb_dest ${gps.area}%%3;
        @setenv{hb_dest;${hb_dest}};
        #var hb_info %%2|${hb_dest};
        @setenv{hb_info;${hb_info}};
        show_to_tuiche ${hb_info};
        autopp.set %%2;
        hb.setup;
        hb.resume;
    } {1};

    #class hb.auto.inner close;

    @declarenv{hb_state;nil};
    @declarenv{mixin_lunci;6};
    @declarenv{hb_lunci;0};
    @declarenv{hb_lunci_total;0};

    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "STARTED" || "${hb_state}" == "TRAVERSE" } {
        #var hb_huoji @getenv{hb_huoji};
        #var hb_dest @getenv{hb_dest};
        #var hb_info @getenv{hb_info};
        #if {"${hb_huoji}" == "nil" || "${hb_dest}" == "nil"} {
            show_to_info 推车失败;
            hb.f;
        };
        #else {
            show_to_info 继续推车;
            show_to_tuiche ${hb_info};
            autopp.set ${hb_huoji};
            #var hb_remaining_path_size 0;
            hb.setup;
            #delay {hb_delay} {
                hb.resume;
            } {5};
        };
    };
    #elseif {"${hb_state}" == "GOT_MIXIN" || "${hb_state}" == "NEXT_MIXIN"} {
        show_to_info 密信;
    };
    #elseif {"${hb_state}" == "nil"} {
        hb.get_owner;
        #math next_lunci @getenv{hb_lunci}+1;
        show_to_quest 推车: @getenv{hb_lunci_total}+${next_lunci}: 准备;
        on_health {hb.q};
    };
    #else {
        hb.b;
    };
};

#alias {hb.stop} {
    log.stop;
    unset_last_quest;
    #var hb_auto 0;
    #class hb.auto.inner kill;
    #undelay {hb_auto_delay};
    #unticker {hb_q_ticker};
    #unticker {hb_ticker};
    dz.cancel;
};

#alias {mx} {
    gt %1;
    on_there {
        on_truce {
            mx.q;
        };
    };
};

#alias {mx.q} {
    #class mx.inner open;
    #nop 你扯开嗓子，开始大喊：小王八蛋，你给我滚出来。;
    #action {一个伙计挖着鼻屎走了出来，道：你找我啥事} {
        do_show 找到密信伙计;
        l;
    };
    #action {财物的伙计 %S(%S)} {
        #format {tmp_huoji} {%l} {%%2};
        #class mx.yao.inner open;
        #action {把一包财物砸向你，一转眼不见了。} {
            @setenv{hb_state;GOT_CAIWU};
            show_to_tuiche 拿到财宝;
            do_show 拿到财宝;
            play get_item.wav;
            #class mx.yao.inner kill;
            #class mx.inner kill;
            on_truce {
                hb.b;
            };
        };
        #action {这里没有这个人} {
            #class mx.yao.inner kill;
        };
        #ticker {yao} {
            ask $tmp_huoji about yao;
        }{0.5};
        #class mx.yao.inner close;
    } {1};
    #class mx.inner close;
    zhaoxun;
};

#alias {hb.p} {
    chain get.san get.qian get.fan get.bao repair;
};

#alias {show_to_tuiche} {
    show_to_quest 推车: @getenv{hb_lunci_total}+@getenv{hb_lunci}: %0;
};

#alias {hb.check_robber} {
    #delay {detect_enemy_delay} {
        #var hb_robber_count ${tmp_robber_count};
        #class detect_enemy.inner kill;
    } {1};
    #math tmp_robber_count ${tmp_robber_count}+1;
    #var tmp_score @mapget{robber_score;%1};
    #if {${tmp_score} > ${robber_score}} {
        #var robber_score ${tmp_score};
        #var enemy_menpai %1;
        #var enemy ${char_id}'s robber ${tmp_robber_count};
    };
};

#alias {hb.resume} {
    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "STARTED"} {
        hb.goto_dest_area;
    };
    #elseif {"${hb_state}" == "TRAVERSE"} {
        hb.traverse;
    };
};

#alias {hb.goto_dest_area} {
    #var hb_paused 0;
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 当前房间未记录, 无法行走};
            };
        };
        #else {
            #math hb_remaining_path_size ${hb_remaining_path_size}-${hb_path_index};
            #var gps.from_roomno ${gps.roomno};
            INVOKE gps.get_room ${hb_dest};
            INVOKE gps.get_path ${gps.from_roomno} ${gps.roomno} ${HB_WEIGHT};
            #var hb_path ${gps_path};
            #var hb_path_index 1;
            #list hb_path size hb_path_size;
            #if {${hb_path_size} == 0} {
                #delay {1} {
                    #show {GPS: 目标房间不可达, 无法行走: ${hb_dest}:${gps.roomno}};
                };
            };
            #else {
                #if {@getenv{mixin_lunci} == 5} {
                    #nop 磨蹭;
                    #math additional_path_size ${hb_remaining_path_size} - ${hb_path_size};
                    #if {${additional_path_size} > 0} {
                        #list hb_path get 3 first_step;
                        #var second_step @opposite{${first_step}};
                        #if {"${second_step}" != "nil"} {
                            #var cnt 0;
                            #while {${cnt} < ${additional_path_size}} {
                                #list hb_path insert 3 ${second_step};
                                #list hb_path insert 3 ${first_step};
                                #math cnt $cnt+2;
                            };
                        };
                        #list hb_path size hb_path_size;
                    };
                };
                hb.run;
            };
        };
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {hb.detect_enemy} {
    #class detect_enemy.inner open;
    #action {~你想要收[1;31m劫匪[2;37;0m为弟子} {
        hb.check_robber quanzhen;
    };
    #action {~你想要收[1;32m劫匪[2;37;0m为弟子} {
        hb.check_robber shaolin;
    };
    #action {~你想要收[1;33m劫匪[2;37;0m为弟子} {
        hb.check_robber gaibang;
    };
    #action {~你想要收[1;34m劫匪[2;37;0m为弟子} {
        hb.check_robber xingxiu;
    };
    #action {~你想要收[1;35m劫匪[2;37;0m为弟子} {
        hb.check_robber huashan;
    };
    #action {~你想要收[1;36m劫匪[2;37;0m为弟子} {
        hb.check_robber taohua;
    };
    #action {~你想要收[1;37m劫匪[2;37;0m为弟子} {
        hb.check_robber wudang;
    };
    #class detect_enemy.inner close;

    #var enemy nil;
    #var tmp_robber_count 0;
    #var hb_robber_count 0;
    #var robber_score 0;
    shou ${char_id}'s robber 1;
    shou ${char_id}'s robber 2;
    shou ${char_id}'s robber 3;
    shou ${char_id}'s robber 4;
    #nop ]]]]]]]]]]]]]];
};

#list hb_blacklist_baowei:荆西镖局 create {
    武当三清殿;少林佛心井;扬州醉仙楼;襄阳当铺;
};

#list hb_blacklist:荆西镖局 create {
    全真天尊讲经堂;
};

#list hb_blacklist:飞马镖局 create {
    晋阳萧府;成都当铺;赞普来福杂货;
};

#list hb_blacklist_baowei:飞马镖局 create {
    襄阳当铺;武当荆西镖局;
};

#list hb_blacklist:飞虎镖局 create {
    晋阳萧府;
};

#list hb_blacklist_baowei:飞虎镖局 create {
    武当荆西镖局;
};

#list hb_blacklist:天福镖局 create {
    嘉兴钱庄;泉州当铺;
};

#list hb_blacklist_baowei:天福镖局 create {
    武当荆西镖局;襄阳当铺;
};

#list hb_blacklist:昌隆镖局 create {
    镇江飞龙镖局;归云庄太湖街;嘉兴钱庄;峨嵋毗卢殿;岳阳车马行;南昌飞虎镖局;泉州当铺;苏州聚宝斋;
};

#class hubiao close
