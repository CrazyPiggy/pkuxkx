#class xue open;
#var xue_count 10;
#alias {xue.start} {
    #class xue.inner open;
    #alias {xue.restart} {
        chifan;
        goto_shifu;
        on_there {
            start_ticker;
        };
    };

    #alias {start_ticker} {
        #ticker {xue} {
            xue ${shifu} for ${xue.gongfu} ${xue_count};
        } {0.5};
    };

    #alias {stop_ticker} {
        #unticker {xue};
    };

    #action {你今天太累了} {
        yun regenerate;
    };

    #action {你的内力不够} {
        stop_ticker;
        #delay {xue_delay} {
            on_there {
                sleep;
            };
            on_wakeup {
                xue.restart;
            };
            goto_rest;
        } {3};
    };

    #action {{你的潜能已经发挥到极限了|没有办法学习这种技能|也许是缺乏实战经验|切磋}} {
        xue.stop;
    };
    #class xue.inner close;

    autoheal.off;
    #var xue.gongfu %1;
    show_to_quest 学习: ${shifu}/${xue.gongfu};
    xue.restart;
};

#alias {xue.stop} {
    #class xue.inner kill;
    #unticker {xue};
    show_to_quest 学习结束;
    autoheal.on;
};
#class xue close;