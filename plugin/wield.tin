#class wield open
#list weapon_types create {blade;sword;staff;whip;spear;hammer;dagger;axe;};
#var wi_freezed 0;
#alias {wi.freeze} {
    #var wi_freezed 1;
};
#alias {wi.unfreeze} {
    #var wi_freezed 0;    
};
#alias {unwi} {
    #if {${wi_freezed} == 0} {
        unwield all;
    };
};

#alias {wi} {
    unwi;
    #if {"${quest}" == "hb" || "${quest}" == "nil" || "${quest}" == ""} {
        #foreach {${weapons_to_wield}[%*]} {weapon} {
            do_wield ${weapon};
        };
    };
};

#alias {do_wield} {
    #var wield_type %1;
    #var wield_count 1;
    #if {"%2" != ""} {
        #var wield_count %2;        
    };
    #if {${wi_freezed} == 0} {
        wield ${${wield_type}_list}[${wield_count}];
    };
};

#alias ss weapons.check;
#var need_reload 0;
#alias {weapons.check} {
    #var need_reload 1;
    #foreach {${weapon_types}[%*]} {weapon_type} {
        #list ${weapon_type}_list clear;
    };
    #list my_weapons clear;
    #list my_special_weapons clear;
    
    #send {id};
    #class weapons.inner open;
    #action {%* = {[^,]+}, {[^,]+},} {
        #var weapon_type %%2;
        #var weapon_name %%3;
        #if {@startwith{${weapon_name};my} == 1} {
            #var need_reload 0;
        };
        #list ${weapon_type}_list size tmp_weapons_size;
        #math tmp_weapons_size ${tmp_weapons_size}+1;
        #if {${tmp_weapons_size} == 1} {
            #list ${weapon_type}_list add ${weapon_name};
            #if {@ismember{${weapon_type};weapon_types} != 0} {
                #if {@startwith{${weapon_name};my} == 1} {
                    #list my_weapons add ${weapon_name};
                };
            };
        };
        #else {
            #list ${weapon_type}_list add {${weapon_name} ${tmp_weapons_size}};
            #if {@ismember{${weapon_type};weapon_types} != 0} {
                #if {@startwith{${weapon_name};my} == 1} {
                    #list my_weapons add {${weapon_name} ${tmp_weapons_size}};
                };
            };
        };
    } {2};
    #action {%* = {[^,]+},} {
        #var weapon_type %%2;
        #var weapon_name %%2;
        #list ${weapon_type}_list size tmp_weapons_size;
        #math tmp_weapons_size ${tmp_weapons_size}+1;
        #if {${tmp_weapons_size} == 1} {
            #list ${weapon_type}_list add ${weapon_name};
        };
        #else {
            #list ${weapon_type}_list add {${weapon_name} ${tmp_weapons_size}};
        };
    } {3};
    #delay {3} {
        #class weapons.inner kill;
        #foreach {${weapon_types}[%*]} {weapon_type} {
            #list special_${weapon_type} size tmp_weapons_size;
            #if {${tmp_weapons_size} != 0} {
                #foreach {${special_${weapon_type}}[%*]} {sw} {
                    #list ${weapon_type}_list add ${sw};
                    #list my_special_weapons add ${sw};
                };                
            };
        };  
    };
    #class weapons.inner close;
};

#alias {on_reload} {
    #alias tmp_on_reload #cr;
    #alias tmp_on_reload %0;
    #action {reloaded} {
        #unaction {reloaded};
        tmp_on_reload;
    };
};

#alias {reload} {
    #alias tmp_reload #cr;
    #alias tmp_reload %0;
    
    #action {好了，你走吧} {
        #unaction {好了，你走吧};
        remove all;
        #delay {3} {
            weapons.check;
        };
        #delay {5} {
            wi;
            we;
            check_naijiu;
            #showme reloaded;
            tmp_reload;
        };
    };
    
    gt 名人堂;
    on_there {
        #send {reload};
    };
};

#class wield close