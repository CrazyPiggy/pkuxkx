#class jianding open
@mapcreate{jianding};
@mapset{jianding;斧;axe};
@mapset{jianding;匕;dagger};
@mapset{jianding;剑;sword};
@mapset{jianding;刀;blade};
@mapset{jianding;杖;staff};
@mapset{jianding;鞭;whip};
@mapset{jianding;枪;spear};
@mapset{jianding;锤;hammer};
@mapset{jianding;铠甲;armor};
@mapset{jianding;盔;head};
@mapset{jianding;腰带;waist};
@mapset{jianding;靴;boots};
@mapset{jianding;项链;necklace};
@mapset{jianding;戒指;ring};
@mapset{jianding;披风;surcoat};
@mapset{jianding;袍;cloth};
@mapset{jianding;护腕;wrists};
@mapset{jianding;手套;hands};
@mapset{jianding;盾;shield};

@mapcreate{jd_value};
@mapset{jd_value;sword;200};
@mapset{jd_value;blade;200};
@mapset{jd_value;staff;200};
@mapset{jd_value;whip;200};
@mapset{jd_value;spear;200};
@mapset{jd_value;hammer;200};
@mapset{jd_value;axe;200};
@mapset{jd_value;dagger;200};

#function {jd_get_type} {
    #if {"%1" == "sword"
    || "%1" == "blade"
    || "%1" == "staff"
    || "%1" == "whip"
    || "%1" == "spear"
    || "%1" == "hammer"
    || "%1" == "dagger"
    || "%1" == "axe"
    } {
        #return weapon;
    };
    #elseif {"%1" == "ring"
    || "%1" == "necklace"
    } {
        #return jewel;
    };
    #else {
        #return armor;
    };
};

#function {jd_get_method} {
    #if {"@jd_get_type{%1}" == "weapon"} {
        #return wield;
    };
    #else {
        #return wear;
    };
};

#alias {jianding.all} {
    #alias tmp_jd #cr;
    #alias tmp_jd %1;
    #class jd.inner open;
    #action {你从包袱中拿出%*之%*。} {
        #var jd_item @mapget{jianding;%%2};
        @stackpush{tmp;${jd_item}};
    };
    #action {之%*(+%*)} {
        #var jd_item @mapget{jianding;%%1};
        #var jd_current_value %%2;
        #nop #var required_value @mapget{jd_value;${jd_item}};
        #var required_value 200;
        unwi;
        remove all;
        #if {"@jd_get_type{${jd_item}}" == "weapon"} {
            #delay {3} {
                #if {${required_value} <= ${jd_current_value}} {
                    notify 掉装备: ${jd_item}:${jd_current_value};
                    put ${jd_item} in bao;
                };
                #else {
                    #showme 卖掉: ${jd_item}:${jd_current_value};
                    sell ${jd_item};
                };
                on_unbusy {
                    jianding.next;
                };
            };
        };
        #else {
            put ${jd_item} in bao;
            on_unbusy {
                jianding.next;
            };
        };
    } {1};

    #class jd.inner close;
    @stackcreate{tmp};
    gt rbz;
    on_there {
        sell all;
        remove all;
        unwield all;
        get all from bao;
        on_unbusy {
            jianding.next;
        };
    };
};

#alias {jianding.next} {
    #var item @stackpop{tmp};
    #if {"${item}" != "nil"} {
        @jd_get_method{${item}} ${item};
        #send {i};
    };
    #else {
        #class jd.inner kill;
        wi;
        we;
        #delay {3} {
            tmp_jd;
        };
    };
};

#action {^你捧着%*之%*上上下下仔仔细细的打量了一遍。} {
    #action {可塑性:%*(%d)} {
        #unaction {可塑性:%*(%d)};
        #if {"%%2" != "0"} {
            #return;
        };
        #var kong %%1;
        #var type @mapget{jianding;%2};
        #var group_type @jd_get_type{${type}};
        #if {"${group_type}" == "jewel"} {
            #if {${kong} > 1} {
                put ${type} in bao;
                notify 掉装备: ${kong} 孔 ${type};
            };
        };
        #elseif {"${group_type}" == "weapon"} {
            put ${type} in bao;
        };
        #else {
            #if {${kong} > 2} {
                put ${type} in bao;
                notify 掉装备: ${kong} 孔 ${type};
            };
        };
    };

    #action {你将一%*之%*放进包袱} {
        #cr;
    };
};

#action {身上掉了出来一%*之%*} {
    jianding @mapget{jianding;%2}
};

#class jianding close