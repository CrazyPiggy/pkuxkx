#class gps_actions open;

#alias gps.clear gps.force_clear;
#alias {gps.force_clear} {
    clear %0;
    on_cleared {
        gps.run;
    };
};

#alias {gps.delay} {
    #var gps_delay 1.5;
    #var gps_delay %1;
    #delay {gps_delay} {
        gps.run;
    } {${gps_delay}};
};

#alias {gps.guohe} {
    #class gps.action.inner open;
    #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {
        #delay {3} {
            yell boat;
        };
    };
    #action {正等着你呢，上来吧。} {
        enter;
    };
    #action {艄公将一块踏脚板搭上堤岸，} {
        enter;
    };
    #alias {shangchuan} {
        #unticker {sw/yell_boat};
        #class gps.action.inner open;
        #action {所有人被赶下了渡船} {
            #class gps.action.inner kill;
            gps.run;
        };
        #action {艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸。} {
            #class gps.action.inner kill;
            out;
            gps.run;
        };
        #class gps.action.inner close;
    };
    #action {^江心驶去} {
        shangchuan;
    };
    #class gps.action.inner close;

    ask shao gong about huanghe;
    ask shao gong about jiang;
    yell boat;
};

#alias {gps.enter_boat} {
    enter boat;
    #action {小舟终于划到近岸，你从船上走了出来。} {
        #unaction {小舟终于划到近岸，你从船上走了出来。};
        gps.run;
    };
    #action {绿衣少女将小船系在树枝之上，你跨上岸去。} {
        #unaction {绿衣少女将小船系在树枝之上，你跨上岸去。};
        gps.run;
    };
    #action {你沿着踏板走了上去} {
        #unaction {你沿着踏板走了上去};
        gps.run;
    };
    #action {你朝船夫挥了挥手便跨上岸去} {
        #unaction {你朝船夫挥了挥手便跨上岸去};
        gps.run;
    };
};

#alias {gps.zuoche} {
    gu;
    qu %1;
    #action {大车停稳了下来} {
        #unaction {大车停稳了下来};
        xia;
        gps.run;
    };
};

#alias {gps.goto_bt} {
    ask qian about 白驼山;
    give qian 1 gold;
    
    #class gps.goto_bt.inner open;
    #ticker {tt} {
        give qian 1 gold;
    } {2};
    
    #action {你身上没有 gold 这样东西。} {
        qu 1 gold;
    };
    
    #action {你拿出一两黄金(Gold)给钱眼开。} {
        #class gps.goto_bt.inner kill;
        #delay {1} {
            gps.run;
        };
    };
    #class gps.goto_bt.inner close;
};
#alias {gps.enter_home} {
    enter ${char_id};
    gps.run;
};

#alias {gps.guo_qiao} {
    #action {你终于一步步的终于挨到了桥头.} {
        #unaction {你终于一步步的终于挨到了桥头.};
        gps.run;
    };
    guo qiao;
};

#alias {gps.wuliang} {
    climb cliff;
    #action {^崖间古松} {
        #unaction {^崖间古松};
        gps.run;
    };
};

#alias {gps.yadong} {
    climb;
    #action {^崖洞} {
        #unaction {^崖洞};
        gps.run;
    };
};

#alias {gps.tiesuo} {
    zou tiesuo;
    #action {你终于来到了对面，心里的石头终于落地。} {
        #unaction {你终于来到了对面，心里的石头终于落地。};
        gps.run;
    };
};

#alias {gps.row} {
    row %1;
    #action {不知过了多久，船终于靠岸了，你累得满头大汗。} {
        #unaction {不知过了多久，船终于靠岸了，你累得满头大汗。};
        gps.run;
    };
};

#alias {gps.busy} {
    on_unbusy {
        gps.run;
    };
};

#alias {gps.maze.tdf} {
    #var current_direction west;
    #list directions create {east;west;north;south};
    #alias {do_look_next} {
        #math look_count ${look_count}+1;
        #if {${look_count} > 4} {
            on_unbusy {
                ${next_direction};
                do_look;
            };    
        };
        #else {
            #list directions get ${look_count} current_direction;
            look ${current_direction};
        };
    };
    
    #alias {do_look} {
        #class gps.tdf.inner open;
        #var next_direction  west;
        #var look_count 0;
        #action {这里的花丛（huacong）比别的地方} {
            #class gps.tdf.inner kill;
            on_unbusy {
                ${current_direction};
                gps.run;            
            };
        } {8};
        
        #action {这里看起来似乎和其他地方不太一样} {
            do_look_next;
        } {8};
        
        #action {心里有种不祥的预感} {
            #var next_direction ${current_direction};
            do_look_next;
        } {9};
        #class gps.tdf.inner close;
        
        do_look_next;
    };
    
    do_look;
};

#alias {gps.maze.yunhai} {
    #class gps.maze.yunhai.inner open;
    #action {^云海出口} {
        #class gps.maze.yunhai.inner kill;
        set brief 1;
        gps.run;
    };
    #ticker {yunhai.ticker} {
        n;w;e;s;e;e;n;
    } {1.5};
    #class gps.maze.yunhai.inner close;
    set brief 2;
    n;w;e;s;e;e;n;
};

#alias {gps.maze.yunhaib} {
    #class gps.maze.yunhai.inner open;
    #action {^云海入口} {
        #class gps.maze.yunhai.inner kill;
        set brief 1;
        gps.run;
    };
    #ticker {yunhai.ticker} {
        e;s;
    } {0.5};
    #class gps.maze.yunhai.inner close;
    set brief 2;
    s;e;s;
};

#alias {gps.muchang} {
    #var next_action %1;
    #class gps.muchang open;
    #action {青海湖畔美不胜收，你不由停下脚步，欣赏起了风景。} {
        #delay {again} {
            ${next_action};            
        } {1};
        #delay {muchang_delay} {
            #class gps.muchang kill;
            gps.run;
        } {2};
    } {1};
    #delay {muchang_delay} {
        #class gps.muchang kill;
        gps.run;
    } {1};
    #class gps.muchang close;
    
    ${next_action};
};

#alias {gps.qu_sibao} {
    #class gps.sibao.inner open;
    #action {^%d%s《{广陵散|率意帖|溪山行旅图|呕血谱}》$} {
        #list sibao add {%%1};
    };
    #act {^柜子里目前存放的物品有：$} {
        #var sibao {};
        #delay {1} {
            #class gps.sibao.inner kill;
            #if {@numitems{sibao}==4} {
                #loop 4 1 i {getout @item{sibao;$i}};
                #delay 1 {
                    gps.run;
                };
            };
            #else {
                #show 你的柜子里四宝不齐或有多余，无法自动前往梅庄。;
            };
        };
    };
    #class gps.sibao.inner close;
    find;
};

#alias {gps.huan_sibao} {
    store ouxue pu;
    store xishan tu;
    store shuaiyi tie;
    store guangling san;
    gps.run;
};

#alias {gps.maze.guolin} {
    #class gps.maze.guolin.inner open;
    #action {^果林$} {
        on_unbusy {
            s;
        };
    };
    #action {^后山$} {
        #class gps.maze.guolin.inner kill;
        gps.run;
    };
    #class gps.maze.guolin.inner close;
    s;
};

#alias {gps.maze.guolinb} {
    #class gps.maze.guolin.inner open;
    #action {^果林$} {
        #if {"${next_direction}" == "s"} {
            #var next_direction e;
        };
        #else {
            #var next_direction s;
        }
        on_unbusy {
            ${next_direction};
        };
    };
    #action {^后山$} {
        on_unbusy {
            s;
        };
    };
    #action {^大道$} {
        #class gps.maze.guolin.inner kill;
        gps.run;
    };
    #class gps.maze.guolin.inner close;
    #var next_direction s;
    ${next_direction};
};

#alias {gps.truce} {
    on_truce {
        gps.run;
    };
};

#alias {gps.climb} {
    #class gps.climb.inner open;
    #action {这里没有任何明显的出路} {
        #class gps.climb.inner kill;
        set brief 1;
        gps.run;
    };
    #action {这里明显的出口} {
        #class gps.climb.inner kill;
        set brief 1;
        gps.run;
    };
    #class gps.climb.inner close;
    set brief 2;
    climb %1;
}
#class gps_actions close;