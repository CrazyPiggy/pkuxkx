#class gps open;
load gps_actions;

#gag gps: get_path_done;
#alias {gps.start} {
    #run {gps} {python3 -m bin.gps.gps};
    #unticker {%*};
    #action {/tmp/%*} {
        #pkuxkx #read %%0;
        #system {rm %%0};
        #pkuxkx #showme gps: get_path_done;
    };
    #pkuxkx;
};

#alias {gps.gt} {
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 当前房间未记录, 无法行走};
            };
        };
        #else {
            #format {epoch_from} {%T};
            #show {GPS: 正在寻路...};
            gps.get_path ${gps.roomno} %1;
        };  
    };
    
    #action {gps: get_path_done} {
        #unaction {gps: get_path_done};
        #format {epoch_to} {%T};
        #show {GPS: 完成寻路, 耗时: @eval{$epoch_to-$epoch_from} 秒};
        pause_screen;
        #var gps_path_index 1;
        #list shortest_path size gps_path_size;
        #if {${gps_path_size} == 0} {
            #delay {1} {
                #show {GPS: 目标房间未记录, 无法行走};
            };
        };
        #else {
            gps.run;
        };        
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {gps.run} {
    #var gps_run_accu 0;
    #var is_break 0;
    #while {${gps_path_index} <= ${gps_path_size}} {
        #math gps_run_accu ${gps_run_accu}+1;
        #if {${gps_run_accu} > 15} {
            #delay {0.5} {
                gps.run;
            };
            #var is_break 1;
            #break;
        };
        #list shortest_path get ${gps_path_index} gps_next_step;
        #math gps_path_index ${gps_path_index}+1;
        ${gps_next_step};
        #if {@startwith{${gps_next_step};gps} == 1} {
            #var is_break 1;
            #break;
        };
    };
    #if {${gps_path_index} > ${gps_path_size} && ${is_break} == 0} {
        #delay {1} {
            #showme {GPS: 抵达目的地};        
        };
    };
};

#alias {gps.get_path} {
    #nop 1. gps.clear 2. gps.guohe 3. gps.delay 4. gps.zuoche;
    #var weights @getenv{gps.weight.clear},@getenv{gps.weight.guohe},@getenv{gps.weight.delay},@getenv{gps.weight.zuoche};
    #gps %1:%2:${weights};
};

#macro {\e[24~} {
    gps.check_link;
};

#nop operate db;
#alias {gps.check_room} {
    #alias tmp_check_room #cr;
    #alias tmp_check_room %1;
    #alias {get_pos_done} {
        #if {@endwith{${pos.room};的甜蜜小屋} == 1} {
            #var gps.roomno 3655;
        };
        #else {
            INVOKE gps.check_room "${pos.area}" "${pos.room}" "${pos.desc}" "${pos.exits}";
        };
        tmp_check_room;
    };
    get_area {
        get_pos {
            get_pos_done;
        };
    };
};

#alias {gps.insert_room} {
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            INVOKE gps.insert_room "${pos.area}" "${pos.room}" "${pos.desc}" "${pos.exits}";
            gps.check_room;
        };
        #else {
            #delay {1} {
                #show {GPS: 房间已记录, 无法再次记录};
            };
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {gps.check_link} {
    #alias {gps_check_room_done} {
        #delay {1} {
            #show {GPS: ${pos.room}: ${gps.roomno}};
        };
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 房间未记录, 无法检查连接};
            };
        };
        #else {
            INVOKE gps.check_link ${gps.roomno};
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};
#alias {gps.insert_link} {
    #var target_room_no %1;
    #var direction %2;
    #var reverse_direction %3;
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 房间未记录, 无法插入连接};
            };
        };
        #else {
            INVOKE gps.insert_link ${gps.roomno} ${target_room_no} "${direction}" "${reverse_direction}"
            gps.check_link;
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};
#class gps close;