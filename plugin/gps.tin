#class gps open;
load gps_actions;

#alias {gps.gt} {
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 当前房间未记录, 无法行走};
            };
        };
        #else {
            gps.get_path ${gps.roomno} %1;
            #var gps_path_index 1;
            #list shortest_path size gps_path_size;
            gps.run;
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {gps.run} {
    #var gps_run_accu 0;
    #while {${gps_path_index} <= ${gps_path_size}} {
        #math gps_run_accu ${gps_run_accu}+1;
        #if {${gps_run_accu} > 15} {
            #delay {0.5} {
                gps.run;
            };
            #break;
        };
        #list shortest_path get ${gps_path_index} gps_next_step;
        #math gps_path_index ${gps_path_index}+1;
        ${gps_next_step};
        #if {@startwith{${gps_next_step};gps} == 1} {
            #break;
        };
    };
};

#alias {gps.get_path} {
    #system {bin/gps/get_path.py %1 %2};
    #read /tmp/gps.tin;
};

#macro {\e[24~} {
    gps.check_link;
};

#nop operate db;
#alias {gps.check_room} {
    #alias tmp_check_room #cr;
    #alias tmp_check_room %1;
    #alias {get_pos_done} {
        #script {roomno} {bin/gps/check_room.py "${pos.area}" "${pos.room}" "${pos.desc}" "${pos.exits}"};
        #var gps.roomno ${roomno}[1];
        #show {GPS: ${pos.room}: ${gps.roomno}};
        tmp_check_room;  
    };
    get_area {
        get_pos {
            get_pos_done;
        };
    };
};

#alias {gps.insert_room} {
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #system {bin/gps/insert_room.py "${pos.area}" "${pos.room}" "${pos.desc}" "${pos.exits}"};
            gps.check_room;
        };
        #else {
            #delay {1} {
                #show {GPS: 房间已记录, 无法再次记录};
            };
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {gps.check_link} {
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 房间未记录, 无法检查连接};
            };
        };
        #else {
            #system {bin/gps/check_link.py ${gps.roomno}};
            #delay {1} {
                #read /tmp/gps.tin;
            };
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};
#alias {gps.insert_link} {
    #var target_room_no %1;
    #var direction %2;
    #var reverse_direction %3;
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 房间未记录, 无法插入连接};
            };
        };
        #else {
            #system {bin/gps/insert_link.py ${gps.roomno} ${target_room_no} ${direction} ${reverse_direction}};
            gps.check_link;
        };  
    };
    gps.check_room {
        gps_check_room_done;
    };
};
#class gps close;