#class tuna open
#var tn_total_count 0;
#var tn_max 95;

#alias {tn.rest} {
    #alias tmp_tn #cr;
    #alias tmp_tn %1;
    #class tn.full.inner open;
    #action {你的精力增加了} {
        tn.rest;
    };
    #ticker {tn} {
        yun regenerate; tuna ${dazuo_count};
    } {1};
    #class tn.full.inner close;
    #math max_jingli ${youxiao_neigong} * 10 - 10;
    #if {${max_jingli} < ${hpbrief_max_jl}} {
        #class tn.full.inner kill;
        tmp_tn;
    };
};

#alias {tn.cancel} {
    #class tn.full.inner kill;
    #unticker {tn};
};

#alias {tn.full} {
    #alias tmp_tn #cr;
    #alias tmp_tn %1;
    #class tn.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #class tn.full.inner close;
    #delay {tn_delay} {
        #ticker {tn} {
            #if {${jl_percent} < ${tn_max}} {
                tuna ${dazuo_count};
            };
            #else {
                #unticker {tn};
                #class tn.full.inner kill;
                #delay {tn_delay} {
                    tmp_tn;
                    #showme tn.full;
                } {3};
            };
        } {1};
    }{1};
};

#alias {tn.start} {
    set_last_quest tn;
    autoheal.off;
    #class tn.inner open;
    #alias {start_ticker} {
        #ticker {tn} {
            tuna ${dazuo_count};
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {tn};
    };

    #action {你现在精不足} {
        stop_ticker;
        sleep;
        on_wakeup {
            start_ticker;
        };
    };

    #action {你的精力修为已经达到了瓶颈} {
        tn.stop;
        show_to_quest 精力到达瓶颈: ${hpbrief_max_jl} => ${max_jingli};
    };

    #action {你的精力上限增加} {
        #math tn_total_count ${tn_total_count}+1;
        show_to_info energy +${tn_total_count};
        #send {jifa};
        #delay {2} {
            tn.check;
        };
    };
    #class tn.inner close;
    #send {jifa};
    gt bs;
    on_there {
        tn.check;
    };
};

#alias {tn.check} {
    stop_ticker;
    chifan;
    burden.check {
        tn.check.inner;
    };
};

#alias {tn.check.inner} {
    #math max_jingli ${youxiao_neigong} * 10 -1;
    #if {${max_jingli} < ${hpbrief_max_jl}} {
        tn.stop;
        #showme tn done;
        show_to_quest 精力到达瓶颈: ${hpbrief_max_jl} => ${max_jingli};
    };
    #else {
        #if {@getburden{酥油茶} < 30} {
            tn.stop;
            chain get.qian get.fan tn.start;
            #return;
        };
        start_ticker;
        show_to_quest 吐纳: ${hpbrief_max_jl} => ${max_jingli};
    };
};

#alias {tn.stop} {
    unset_last_quest;
    autoheal.on;
    stop_ticker;
    show_to_quest 吐纳完毕;
    #class tn.inner kill;
};
#class tuna close