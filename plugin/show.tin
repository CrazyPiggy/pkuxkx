#class show open;
#split 19 1

#nop hp/-/status/-/shortcut/-/5(info)/-/6(chat)/- = 19
#var info_window {{1}{ } {2}{ } {3}{ } {4}{ } {5}{ }};
#var chat_window {{1}{ } {2}{ } {3}{ } {4}{ } {5}{ } {6}{ }};

#var info_window_offset 6;
#math chat_window_offset $info_window_offset+&info_window[]+1;

#alias {show.reset} {
    #split 19 1;
    #var info_window {{1}{ } {2}{ } {3}{ } {4}{ } {5}{ }};
    #var chat_window {{1}{ } {2}{ } {3}{ } {4}{ } {5}{ } {6}{ }};
    show.init;
};

#alias {show.init} {
    #alias {show_to_status} {
        #var show_status %%0;
        update_status_line;
    };

    show_to_shortcut ${shortcut};
    #loop {1} {&info_window[]} {cnt}
    {
        #math pos $cnt+${info_window_offset};
        #echo {{<aff>$info_window[$cnt]<099>} {-$pos}};
    }

    #loop {1} {&chat_window[]} {cnt}
    {
        #math pos $cnt+${chat_window_offset};
        #echo {{<aff>$chat_window[$cnt]<099>} {-$pos}};
    }
};

#alias {show_to_shortcut} {
    #var show_key %0;
    #echo {{<cfa>${show_key}<099>}{-5}};
};

#alias {show_to_info} {
    #format {tmp_time} {%t} {%T};
    #var screen_width @screenw{};
    #math header_width @len{tmp_time}+2;
    #var temp ${tmp_time} <099> %0;

    #if {@len{${temp}} > ${screen_width}} {
        #math split_chunk_size ${screen_width}-${header_width}-10;
        INVOKE split_string "%0" ${split_chunk_size};
        #foreach {${split_string}[%*]} {item} {
            show_to_info > ${item};
        };
        #return;
    };
    #list info_window ins -1 $temp;
    #list info_window del 1;

    #loop {1} {&info_window[]} {info_cnt}
    {
        #math info_pos $info_cnt+${info_window_offset};
        #echo {{<aff>$info_window[$info_cnt]<099>} {-$info_pos}};
    }
};

#var showing_to_chat 0;
#alias {show_to_chat} {
    #if {${showing_to_chat} == 1} {
        #delay {0.2} {
            show_to_chat %0;
        };
        #return;
    };
    #format {tmp_time} {%t} {%T};
    #var screen_width @screenw{};
    #math header_width @len{tmp_time}+2;
    #var temp ${tmp_time} <099> %0;

    #if {@len{${temp}} > ${screen_width}} {
        #math split_chunk_size (${screen_width}-${header_width}-20)/2;
        INVOKE split_string "%0" ${split_chunk_size};
        #foreach {${split_string}[%*]} {item} {
            show_to_chat > ${item};
        };
        #return;
    };

    #var showing_to_chat 1;
    #list chat_window ins -1 {$temp};
    #list chat_window del 1;

    #loop {1} {&chat_window[]} {chat_cnt}
    {
        #math chat_pos $chat_cnt+${chat_window_offset};
        #echo {{<aff>$chat_window[$chat_cnt]<099>} {-$chat_pos}};
    }
    #var showing_to_chat 0;
};

#alias {update_status_line} {
    #echo {{<099>${show_status}<099> => <cfa>${show_quest}<099>}{-1}};
};

#alias {show_to_quest} {
    #var show_quest %0;
    update_status_line;
};

#list chat_ignore_list create {
    萧峰(xiao feng)告诉你;
    我找不到,请看其他的回复;
    连续的重复查询是不起作用的;
    快去摁死它吧;
    并不存在;
    青山不改，绿水常流，咱们后会有期;
    这封信用特殊药水写就;
    钱眼开记完帐;
    暂时不能处理你的询问,请稍等;
    总舵主把狗皇帝扣在六和塔上了;
    蒙哥对着;
    快来快来，肥水不留外人田，有好事当然想着你了;
};

#foreach {${chat_ignore_list}[%*]} {chat_ignore} {
    #action {${chat_ignore}} {
        #cr;
    } {2};
};

#gag {【闲聊】%*};
#action {^【闲聊】%*} {
    show_to_chat %0;
    #if {${xmpp_chat} == 1} {
        #xmpp %0;
    };
} {2};

#action {%1{回答|告诉}你：%2} {
    show_to_chat 【私聊】%0;
    #xmpp 【私聊】From %1：%2;
} {3};

#action {你{回答|告诉}%1：%2} {
    show_to_chat 【私聊】%0;
    #xmpp 【私聊】To %1：%2;
} {3};
#class show close;
